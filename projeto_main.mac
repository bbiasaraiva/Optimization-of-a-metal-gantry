/CLEAR

!   1. Definição do nome do ficheiro e do título do problema
/filname, ex5
/title, EX5


!   2. Definição das variáveis
meshsize=0.4    !definição do tamanho da malha
b=0.28  !largura do banso
tf=0.0247  !espessura do banso
tw=0.004  !espessura da alma
L=10
zi=0
zf=3
x0=0
xf=20
h=0.3
h1=0.9*h-(2*tf)!largura da alma 1 
h2=0.2*h1 !largura da alma 2

densidade=7850


/PREP7
*get,tcpubefore,active,,time,CPU !para calcular o tempo da solução

/VUP,1,Z        !alterar a vista, considerar Z para cima
ET,1,SHELL181       !definição do elemento finito de casca shell 181

/vup, 1, Z

! 3. definção das propriedades materiais
mptemp, 1
mpdata, EX, 1, , 210000000
mpdata, PRXY, 1, , 0.3

!   4. Definição da geometria

SECTYPE,1,SHELL,,tf
SECDATA,tf,1,,

SECTYPE ,2,SHELL,,tw
SECDATA,tw,1,,

projeto_pilar,h2,tf,b,h1,zi,zf,x0
projeto_pilar,h2,tf,b,h1,zi,zf,L*2

projeto_viga,L,h1,h2,b,zf,L*2-(h1/2)-tf/2
projeto_viga,L,h1,h2,b,zf,x0+h1/2+tf/2

projeto_canto,zf,h2,h1,b,x0,tf
projeto_canto,zf,h2,h1,b,L*2,tf

allsel
nummrg,ALL,,,,


!volumes

!pilares
!volume dos retangulos de cima/baixo
c_p=((-(h2/2)+(h1/2))**2+zf**2)**0.5
vol1_p=2*(c_p*b)*tf
!volume meio
vol2_p=((h1+h2)*zf/2)*tw
vol_p=2*(vol1_p+vol2_p)


!viga
! volume dos retangulos de cima/baixo
c_v=((h1-h2)**2+(L)**2)**0.5
vol1_v=2*(c_v*b)*tf
!volume meio
vol2_v=((h1+h2)*(L)/2)*tw
vol_v=2*(vol1_v+vol2_v)


!cantos
v_extr=2*b*h2*tf
v_cb=2*b*(h1+(2*tf))*tf
v_meio=(h1+(2*tf))*(h2+(2*tf))*tw
vol_c=v_extr+v_cb*v_meio

!volume total
vol_total=vol_p+vol_v+vol_c

!peso do portico
peso_portico=densidade*vol_total

ESIZE,meshsize,0,   
MSHKEY,1            

allsel       

amesh,all  

!bases fixas
ksel,s,loc,z,0
DK,ALL, , , ,0,ALL, , , , , , 

allsel
!-20kN FZ a dividir pelos 12 kp dos cantos que vao levar com a carga
KSEL,s,loc,z,zf+h2
FK,ALL,FZ,-20/12

allsel
! -100kN FZ a dividir pelos 3 kp dos meio vao da viga que vao levar com a carga
KSEL,s,loc,z,zf+h1
FK,ALL,FZ,-100/3


allsel

!solve static para determinar a flecha
/SOLU
ALLSEL		
ANTYPE,STATIC
PSTRES,ON
SOLVE
FINISH
!plot da solution da flecha
/POST1 
SET,FIRST 

PLNSOL,U,Z,0,1.0

*GET,flecha,PLNS,,MAX
!novo  solve buckle para determinar o fator
/SOLU
ANTYPE,BUCKLE
BUCOPT,LANB,1,0,100000,RANGE 
MXPAND,1,0,0,1,0.001, 
SOLVE
FINISH

*get,tcpuafter,active,,time,CPU 
solution_time=(tcpuafter-tcpubefore)
!plot da solution do fator
/POST1 
SET,FIRST 

PLNSOL, U,SUM, 0,1.0

*GET,fact,MODE,1,FREQ

/EFACET,1 
/VIEW,1,1,1,1 
/ANG,1 

/com,%fact%

/com,%flecha%

/com, %solution_time%
